if (rootProject.ext.isAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply from:"../app_base.gradle"
apply plugin: 'org.greenrobot.greendao'

android {
    if (!rootProject.ext.isAlone) {
        resourcePrefix "comic_"
    }

    defaultConfig {
        if (rootProject.ext.isAlone) {
            applicationId "com.wr.comic"
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.isAlone) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/module/java']
                res.srcDirs = ['src/main/res', 'src/main/module/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
    greendao {
        schemaVersion 1  //版本
        targetGenDir 'src/main/java' //生成文件路径
        daoPackage 'com.wr.comic.dao.gen' // 一般为app包名+生成文件的文件夹名
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':base')
    implementation project(':common')

    api 'com.youth.banner:banner:1.4.10'
    api 'org.jsoup:jsoup:1.9.2'
}



